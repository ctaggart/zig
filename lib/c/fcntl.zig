const builtin = @import("builtin");

const std = @import("std");
const linux = std.os.linux;
const off_t = linux.off_t;

const symbol = @import("../c.zig").symbol;
const errno = @import("../c.zig").errno;

comptime {
    if (builtin.target.isWasiLibC()) {
        symbol(&creatWasi, "creat");
        symbol(&creatWasi, "creat64");
    }
    if (builtin.target.isMuslLibC()) {
        symbol(&creatLinux, "creat");
        symbol(&fcntlLinux, "fcntl");
        symbol(&openLinux, "open");
        symbol(&openatLinux, "openat");

        symbol(&fallocateLinux, "fallocate");
        symbol(&posix_fadviseLinux, "posix_fadvise");
        symbol(&posix_fallocateLinux, "posix_fallocate");
    }
}

fn creatLinux(path: [*:0]const c_char, mode: linux.mode_t) callconv(.c) c_int {
    return openLinux(path, @bitCast(@as(u32, @bitCast(std.c.O{ .ACCMODE = .WRONLY, .CREAT = true, .TRUNC = true }))), mode);
}

extern "c" fn __wasilibc_open_nomode(path: [*:0]const c_char, flags: c_int) callconv(.c) c_int;

fn creatWasi(path: [*:0]const c_char, mode: c_uint) callconv(.c) c_int {
    _ = mode;
    const O_CREAT: c_int = 1 << 12;
    const O_TRUNC: c_int = 8 << 12;
    const O_WRONLY: c_int = 0x10000000;
    return __wasilibc_open_nomode(path, O_CREAT | O_WRONLY | O_TRUNC);
}

fn fcntlLinux(fd: c_int, cmd: c_int, arg: c_ulong) callconv(.c) c_int {
    var a = arg;
    // F_SETFL must include O_LARGEFILE (musl always uses 64-bit offsets)
    if (cmd == std.c.F.SETFL) a |= 0o100000; // O_LARGEFILE
    return errno(linux.fcntl(fd, cmd, a));
}

fn openLinux(path: [*:0]const c_char, flags: c_int, mode: linux.mode_t) callconv(.c) c_int {
    return errno(linux.openat(linux.AT.FDCWD, @ptrCast(path), @bitCast(flags), mode));
}

fn openatLinux(fd: c_int, path: [*:0]const c_char, flags: c_int, mode: linux.mode_t) callconv(.c) c_int {
    return errno(linux.openat(fd, @ptrCast(path), @bitCast(flags), mode));
}
fn fallocateLinux(fd: c_int, mode: c_int, offset: off_t, len: off_t) callconv(.c) c_int {
    return errno(linux.fallocate(fd, mode, offset, len));
}

fn posix_fadviseLinux(fd: c_int, offset: off_t, len: off_t, advice: c_int) callconv(.c) c_int {
    return errno(linux.fadvise(fd, offset, len, @intCast(advice)));
}

fn posix_fallocateLinux(fd: c_int, offset: off_t, len: off_t) callconv(.c) c_int {
    // posix_fallocate returns error code directly (NOT -1 with errno)
    const r = linux.fallocate(fd, 0, offset, len);
    const signed: isize = @bitCast(r);
    if (signed < 0) return @intCast(-signed);
    return 0;
}
