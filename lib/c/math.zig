const builtin = @import("builtin");

const std = @import("std");
const math = std.math;
const expect = std.testing.expect;
const expectEqual = std.testing.expectEqual;
const expectApproxEqAbs = std.testing.expectApproxEqAbs;
const expectApproxEqRel = std.testing.expectApproxEqRel;

const symbol = @import("../c.zig").symbol;

comptime {
    if (builtin.target.isMinGW()) {
        symbol(&isnan, "isnan");
        symbol(&isnan, "__isnan");
        symbol(&isnanf, "isnanf");
        symbol(&isnanf, "__isnanf");
        symbol(&isnanl, "isnanl");
        symbol(&isnanl, "__isnanl");

        symbol(&math.floatTrueMin(f64), "__DENORM");
        symbol(&math.inf(f64), "__INF");
        symbol(&math.nan(f64), "__QNAN");
        symbol(&math.snan(f64), "__SNAN");

        symbol(&math.floatTrueMin(f32), "__DENORMF");
        symbol(&math.inf(f32), "__INFF");
        symbol(&math.nan(f32), "__QNANF");
        symbol(&math.snan(f32), "__SNANF");

        symbol(&math.floatTrueMin(c_longdouble), "__DENORML");
        symbol(&math.inf(c_longdouble), "__INFL");
        symbol(&math.nan(c_longdouble), "__QNANL");
        symbol(&math.snan(c_longdouble), "__SNANL");
    }

    if (builtin.target.isMinGW() or builtin.target.isMuslLibC() or builtin.target.isWasiLibC()) {
        symbol(&coshf, "coshf");
        symbol(&hypotf, "hypotf");
        symbol(&hypotl, "hypotl");
        symbol(&modff, "modff");
        symbol(&modfl, "modfl");
        symbol(&nan, "nan");
        symbol(&nanf, "nanf");
        symbol(&nanl, "nanl");
        symbol(&tanhf, "tanhf");
    }

    if (builtin.target.isMuslLibC() or builtin.target.isWasiLibC()) {
        symbol(&acos, "acos");
        symbol(&acosf, "acosf");
        symbol(&acoshf, "acoshf");
        symbol(&asin, "asin");
        symbol(&atan, "atan");
        symbol(&atanf, "atanf");
        symbol(&atanl, "atanl");
        symbol(&cbrt, "cbrt");
        symbol(&cbrtf, "cbrtf");
        symbol(&cosh, "cosh");
        symbol(&exp10, "exp10");
        symbol(&exp10f, "exp10f");
        symbol(&fdim_, "fdim");
        symbol(&fdimf_, "fdimf");
        symbol(&fdiml_, "fdiml");
        symbol(&finite_, "finite");
        symbol(&finitef_, "finitef");
        symbol(&hypot, "hypot");
        symbol(&ldexp_, "ldexp");
        symbol(&ldexpf_, "ldexpf");
        symbol(&ldexpl_, "ldexpl");
        symbol(&modf, "modf");
        symbol(&pow, "pow");
        symbol(&pow10, "pow10");
        symbol(&pow10f, "pow10f");
        symbol(&scalbln_, "scalbln");
        symbol(&scalblnf_, "scalblnf");
        symbol(&scalblnl_, "scalblnl");
        symbol(&scalbn_, "scalbn");
        symbol(&scalbnf_, "scalbnf");
        symbol(&scalbnl_, "scalbnl");
        symbol(&tanh, "tanh");
    }

    if (builtin.target.isMuslLibC()) {
        symbol(&copysign, "copysign");
        symbol(&copysignf, "copysignf");
        symbol(&fpclassify_, "__fpclassify");
        symbol(&fpclassifyf_, "__fpclassifyf");
        symbol(&fpclassifyl_, "__fpclassifyl");
        symbol(&rint, "rint");
        symbol(&signbit_, "__signbit");
        symbol(&signbitf_, "__signbitf");
        symbol(&signbitl_, "__signbitl");
    }

    symbol(&copysignl, "copysignl");
}

fn acos(x: f64) callconv(.c) f64 {
    return math.acos(x);
}

fn acosf(x: f32) callconv(.c) f32 {
    return math.acos(x);
}

fn acoshf(x: f32) callconv(.c) f32 {
    return math.acosh(x);
}

fn asin(x: f64) callconv(.c) f64 {
    return math.asin(x);
}

fn atan(x: f64) callconv(.c) f64 {
    return math.atan(x);
}

fn atanf(x: f32) callconv(.c) f32 {
    return math.atan(x);
}

fn atanl(x: c_longdouble) callconv(.c) c_longdouble {
    return switch (@typeInfo(@TypeOf(x)).float.bits) {
        16 => math.atan(@as(f16, @floatCast(x))),
        32 => math.atan(@as(f32, @floatCast(x))),
        64 => math.atan(@as(f64, @floatCast(x))),
        80 => math.atan(@as(f80, @floatCast(x))),
        128 => math.atan(@as(f128, @floatCast(x))),
        else => unreachable,
    };
}

fn cbrt(x: f64) callconv(.c) f64 {
    return math.cbrt(x);
}

fn cbrtf(x: f32) callconv(.c) f32 {
    return math.cbrt(x);
}

fn copysign(x: f64, y: f64) callconv(.c) f64 {
    return math.copysign(x, y);
}

fn copysignf(x: f32, y: f32) callconv(.c) f32 {
    return math.copysign(x, y);
}

fn copysignl(x: c_longdouble, y: c_longdouble) callconv(.c) c_longdouble {
    return math.copysign(x, y);
}

fn cosh(x: f64) callconv(.c) f64 {
    return math.cosh(x);
}

fn coshf(x: f32) callconv(.c) f32 {
    return math.cosh(x);
}

// FP classification constants matching musl math.h
const FP_NAN: c_int = 0;
const FP_INFINITE: c_int = 1;
const FP_ZERO: c_int = 2;
const FP_SUBNORMAL: c_int = 3;
const FP_NORMAL: c_int = 4;

fn fpclassifyGeneric(comptime T: type, x: T) c_int {
    if (math.isNan(x)) return FP_NAN;
    if (math.isInf(x)) return FP_INFINITE;
    if (x == 0) return FP_ZERO;
    if (!math.isNormal(x)) return FP_SUBNORMAL;
    return FP_NORMAL;
}

fn fpclassify_(x: f64) callconv(.c) c_int {
    return fpclassifyGeneric(f64, x);
}

fn fpclassifyf_(x: f32) callconv(.c) c_int {
    return fpclassifyGeneric(f32, x);
}

fn fpclassifyl_(x: c_longdouble) callconv(.c) c_int {
    return fpclassifyGeneric(c_longdouble, x);
}

test "fpclassify" {
    try expectEqual(FP_ZERO, fpclassifyGeneric(f64, @as(f64, 0.0)));
    try expectEqual(FP_ZERO, fpclassifyGeneric(f64, @as(f64, -0.0)));
    try expectEqual(FP_NORMAL, fpclassifyGeneric(f64, @as(f64, 1.0)));
    try expectEqual(FP_NORMAL, fpclassifyGeneric(f64, @as(f64, -1.0)));
    try expectEqual(FP_INFINITE, fpclassifyGeneric(f64, math.inf(f64)));
    try expectEqual(FP_INFINITE, fpclassifyGeneric(f64, -math.inf(f64)));
    try expectEqual(FP_NAN, fpclassifyGeneric(f64, math.nan(f64)));
    try expectEqual(FP_SUBNORMAL, fpclassifyGeneric(f64, math.floatTrueMin(f64)));
    try expectEqual(FP_SUBNORMAL, fpclassifyGeneric(f64, -math.floatTrueMin(f64)));

    try expectEqual(FP_ZERO, fpclassifyGeneric(f32, @as(f32, 0.0)));
    try expectEqual(FP_NORMAL, fpclassifyGeneric(f32, @as(f32, 1.0)));
    try expectEqual(FP_INFINITE, fpclassifyGeneric(f32, math.inf(f32)));
    try expectEqual(FP_NAN, fpclassifyGeneric(f32, math.nan(f32)));
    try expectEqual(FP_SUBNORMAL, fpclassifyGeneric(f32, math.floatTrueMin(f32)));
}

fn exp10(x: f64) callconv(.c) f64 {
    return math.pow(f64, 10.0, x);
}

fn exp10f(x: f32) callconv(.c) f32 {
    return math.pow(f32, 10.0, x);
}

fn fdimGeneric(comptime T: type, x: T, y: T) T {
    if (math.isNan(x)) return x;
    if (math.isNan(y)) return y;
    return if (x > y) x - y else @as(T, 0.0);
}

fn fdim_(x: f64, y: f64) callconv(.c) f64 {
    return fdimGeneric(f64, x, y);
}

fn fdimf_(x: f32, y: f32) callconv(.c) f32 {
    return fdimGeneric(f32, x, y);
}

fn fdiml_(x: c_longdouble, y: c_longdouble) callconv(.c) c_longdouble {
    return fdimGeneric(c_longdouble, x, y);
}

test "fdim" {
    try expectEqual(@as(f64, 3.0), fdim_(@as(f64, 5.0), @as(f64, 2.0)));
    try expectEqual(@as(f64, 0.0), fdim_(@as(f64, 2.0), @as(f64, 5.0)));
    try expect(math.isNan(fdim_(math.nan(f64), @as(f64, 1.0))));
    try expect(math.isNan(fdim_(@as(f64, 1.0), math.nan(f64))));
    try expectEqual(@as(f32, 3.0), fdimf_(@as(f32, 5.0), @as(f32, 2.0)));
    try expectEqual(@as(f32, 0.0), fdimf_(@as(f32, 2.0), @as(f32, 5.0)));
}

fn finite_(x: f64) callconv(.c) c_int {
    return if (math.isFinite(x)) 1 else 0;
}

fn finitef_(x: f32) callconv(.c) c_int {
    return if (math.isFinite(x)) 1 else 0;
}

test "finite" {
    try expectEqual(@as(c_int, 1), finite_(@as(f64, 1.0)));
    try expectEqual(@as(c_int, 1), finite_(@as(f64, 0.0)));
    try expectEqual(@as(c_int, 0), finite_(math.inf(f64)));
    try expectEqual(@as(c_int, 0), finite_(-math.inf(f64)));
    try expectEqual(@as(c_int, 0), finite_(math.nan(f64)));
}

fn hypot(x: f64, y: f64) callconv(.c) f64 {
    return math.hypot(x, y);
}

fn hypotf(x: f32, y: f32) callconv(.c) f32 {
    return math.hypot(x, y);
}

fn hypotl(x: c_longdouble, y: c_longdouble) callconv(.c) c_longdouble {
    return math.hypot(x, y);
}

fn isnan(x: f64) callconv(.c) c_int {
    return if (math.isNan(x)) 1 else 0;
}

fn isnanf(x: f32) callconv(.c) c_int {
    return if (math.isNan(x)) 1 else 0;
}

fn isnanl(x: c_longdouble) callconv(.c) c_int {
    return if (math.isNan(x)) 1 else 0;
}

fn modfGeneric(comptime T: type, x: T, iptr: *T) T {
    if (math.isNegativeInf(x)) {
        iptr.* = -math.inf(T);
        return -0.0;
    }

    if (math.isPositiveInf(x)) {
        iptr.* = math.inf(T);
        return 0.0;
    }

    if (math.isNan(x)) {
        iptr.* = math.nan(T);
        return math.nan(T);
    }

    const r = math.modf(x);
    iptr.* = r.ipart;

    // If the result is a negative zero, we must be explicit about
    // returning a negative zero.
    return if (math.isNegativeZero(x) or (x < 0.0 and x == r.ipart)) -0.0 else r.fpart;
}

fn modf(x: f64, iptr: *f64) callconv(.c) f64 {
    return modfGeneric(f64, x, iptr);
}

fn modff(x: f32, iptr: *f32) callconv(.c) f32 {
    return modfGeneric(f32, x, iptr);
}

fn modfl(x: c_longdouble, iptr: *c_longdouble) callconv(.c) c_longdouble {
    return modfGeneric(c_longdouble, x, iptr);
}

fn testModf(comptime T: type) !void {
    // Choose the appropriate `modf` impl to test based on type
    const f = switch (T) {
        f32 => modff,
        f64 => modf,
        c_longdouble => modfl,
        else => @compileError("modf not implemented for " ++ @typeName(T)),
    };

    var int: T = undefined;
    const iptr = &int;
    const eps_val: comptime_float = @max(1e-6, math.floatEps(T));

    const normal_frac = f(@as(T, 1234.567), iptr);
    // Account for precision error
    const expected = 1234.567 - @as(T, 1234);
    try expectApproxEqAbs(expected, normal_frac, eps_val);
    try expectApproxEqRel(@as(T, 1234.0), iptr.*, eps_val);

    // When `x` is a NaN, NaN is returned and `*iptr` is set to NaN
    const nan_frac = f(math.nan(T), iptr);
    try expect(math.isNan(nan_frac));
    try expect(math.isNan(iptr.*));

    // When `x` is positive infinity, +0 is returned and `*iptr` is set to
    // positive infinity
    const pos_zero_frac = f(math.inf(T), iptr);
    try expect(math.isPositiveZero(pos_zero_frac));
    try expect(math.isPositiveInf(iptr.*));

    // When `x` is negative infinity, -0 is returned and `*iptr` is set to
    // negative infinity
    const neg_zero_frac = f(-math.inf(T), iptr);
    try expect(math.isNegativeZero(neg_zero_frac));
    try expect(math.isNegativeInf(iptr.*));

    // Return -0 when `x` is a negative integer
    const nz_frac = f(@as(T, -1000.0), iptr);
    try expect(math.isNegativeZero(nz_frac));
    try expectEqual(@as(T, -1000.0), iptr.*);

    // Return +0 when `x` is a positive integer
    const pz_frac = f(@as(T, 1000.0), iptr);
    try expect(math.isPositiveZero(pz_frac));
    try expectEqual(@as(T, 1000.0), iptr.*);
}

test "modf" {
    try testModf(f32);
    try testModf(f64);
    try testModf(c_longdouble);
}

fn ldexp_(x: f64, n: c_int) callconv(.c) f64 {
    return math.ldexp(x, n);
}

fn ldexpf_(x: f32, n: c_int) callconv(.c) f32 {
    return math.ldexp(x, n);
}

fn ldexpl_(x: c_longdouble, n: c_int) callconv(.c) c_longdouble {
    return math.ldexp(x, n);
}

test "ldexp" {
    try expectEqual(@as(f64, 4.0), ldexp_(@as(f64, 1.0), 2));
    try expectEqual(@as(f64, 0.5), ldexp_(@as(f64, 1.0), -1));
    try expectEqual(@as(f64, 0.0), ldexp_(@as(f64, 0.0), 10));
    try expect(math.isNan(ldexp_(math.nan(f64), 0)));
    try expect(math.isPositiveInf(ldexp_(math.inf(f64), 1)));

    try expectEqual(@as(f32, 4.0), ldexpf_(@as(f32, 1.0), 2));
    try expectEqual(@as(f32, 0.5), ldexpf_(@as(f32, 1.0), -1));
}

fn nan(_: [*:0]const c_char) callconv(.c) f64 {
    return math.nan(f64);
}

fn nanf(_: [*:0]const c_char) callconv(.c) f32 {
    return math.nan(f32);
}

fn nanl(_: [*:0]const c_char) callconv(.c) c_longdouble {
    return math.nan(c_longdouble);
}

fn pow(x: f64, y: f64) callconv(.c) f64 {
    return math.pow(f64, x, y);
}

fn pow10(x: f64) callconv(.c) f64 {
    return exp10(x);
}

fn pow10f(x: f32) callconv(.c) f32 {
    return exp10f(x);
}

fn scalblnGeneric(comptime T: type, x: T, n: c_long) T {
    const clamped: i32 = if (n > math.maxInt(i32))
        math.maxInt(i32)
    else if (n < math.minInt(i32))
        math.minInt(i32)
    else
        @intCast(n);
    return math.ldexp(x, clamped);
}

fn scalbln_(x: f64, n: c_long) callconv(.c) f64 {
    return scalblnGeneric(f64, x, n);
}

fn scalblnf_(x: f32, n: c_long) callconv(.c) f32 {
    return scalblnGeneric(f32, x, n);
}

fn scalblnl_(x: c_longdouble, n: c_long) callconv(.c) c_longdouble {
    return scalblnGeneric(c_longdouble, x, n);
}

fn scalbn_(x: f64, n: c_int) callconv(.c) f64 {
    return math.ldexp(x, n);
}

fn scalbnf_(x: f32, n: c_int) callconv(.c) f32 {
    return math.ldexp(x, n);
}

fn scalbnl_(x: c_longdouble, n: c_int) callconv(.c) c_longdouble {
    return math.ldexp(x, n);
}

test "scalbn" {
    try expectEqual(@as(f64, 24.0), scalbn_(@as(f64, 1.5), 4));
    try expectEqual(@as(f32, 24.0), scalbnf_(@as(f32, 1.5), 4));
    try expectEqual(@as(f64, 24.0), scalbln_(@as(f64, 1.5), 4));
    try expectEqual(@as(f32, 24.0), scalblnf_(@as(f32, 1.5), 4));
}

fn signbit_(x: f64) callconv(.c) c_int {
    return @intFromBool(math.signbit(x));
}

fn signbitf_(x: f32) callconv(.c) c_int {
    return @intFromBool(math.signbit(x));
}

fn signbitl_(x: c_longdouble) callconv(.c) c_int {
    return @intFromBool(math.signbit(x));
}

test "signbit" {
    try expectEqual(@as(c_int, 0), signbit_(@as(f64, 1.0)));
    try expectEqual(@as(c_int, 1), signbit_(@as(f64, -1.0)));
    try expectEqual(@as(c_int, 0), signbit_(@as(f64, 0.0)));
    try expectEqual(@as(c_int, 1), signbit_(@as(f64, -0.0)));
    try expectEqual(@as(c_int, 0), signbitf_(@as(f32, 1.0)));
    try expectEqual(@as(c_int, 1), signbitf_(@as(f32, -1.0)));
}

fn rint(x: f64) callconv(.c) f64 {
    const toint: f64 = 1.0 / @as(f64, math.floatEps(f64));
    const a: u64 = @bitCast(x);
    const e = a >> 52 & 0x7ff;
    const s = a >> 63;
    var y: f64 = undefined;

    if (e >= 0x3ff + 52) {
        return x;
    }
    if (s == 1) {
        y = x - toint + toint;
    } else {
        y = x + toint - toint;
    }
    if (y == 0) {
        return if (s == 1) -0.0 else 0;
    }
    return y;
}

test "rint" {
    // Positive numbers round correctly
    try expectEqual(@as(f64, 42.0), rint(42.2));
    try expectEqual(@as(f64, 42.0), rint(41.8));

    // Negative numbers round correctly
    try expectEqual(@as(f64, -6.0), rint(-5.9));
    try expectEqual(@as(f64, -6.0), rint(-6.1));

    // No rounding needed test
    try expectEqual(@as(f64, 5.0), rint(5.0));
    try expectEqual(@as(f64, -10.0), rint(-10.0));
    try expectEqual(@as(f64, 0.0), rint(0.0));

    // Very large numbers return unchanged
    const large: f64 = 9007199254740992.0; // 2^53
    try expectEqual(large, rint(large));
    try expectEqual(-large, rint(-large));

    // Small positive numbers round to zero
    const pos_result = rint(0.3);
    try expectEqual(@as(f64, 0.0), pos_result);
    try expect(@as(u64, @bitCast(pos_result)) == 0);

    // Small negative numbers round to negative zero
    const neg_result = rint(-0.3);
    try expectEqual(@as(f64, 0.0), neg_result);
    const bits: u64 = @bitCast(neg_result);
    try expect((bits >> 63) == 1);

    // Exact half rounds to nearest even (banker's rounding)
    try expectEqual(@as(f64, 2.0), rint(2.5));
    try expectEqual(@as(f64, 4.0), rint(3.5));
}

fn tanh(x: f64) callconv(.c) f64 {
    return math.tanh(x);
}

fn tanhf(x: f32) callconv(.c) f32 {
    return math.tanh(x);
}
